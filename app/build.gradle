buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


def keystorePropertiesFile = file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId "com.example.trafimau_app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 600
        versionName "6.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.config

            ext.betaDistributionGroupAliases = 'mds2019'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Fabric library
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    // AppEntity Center library
    def appCenterSdkVersion = '1.11.2'
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    // Appmetrica SDK library
    implementation 'com.yandex.android:mobmetricalib:3.5.3'
    implementation "com.yandex.android:mobmetricapushlib:1.3.0"

    //  Google Play Install Referrer improves tracking of installation sources
    implementation 'com.android.installreferrer:installreferrer:1.0'

    // for AppMetrica push campaigns
    implementation "com.google.firebase:firebase-messaging:17.4.0"
    implementation "com.google.android.gms:play-services-base:16.1.0"
    implementation "com.android.support:support-compat:28.0.0"

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.13.1'

    // room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    // debug room database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.5'

    // firebase
    implementation 'com.google.firebase:firebase-core:16.0.1'
}

apply plugin: 'com.noveogroup.android.check'

// Set configuration for Checkstyle, Findbugs, PMD.
// If no configuration provided for those checkers
// defaults will be set
check {
    skip false
    abortOnError false

//    Checkstyle causes java.lang.NoSuchFieldError: WHITESPACE
//    so disable it.
    checkstyle {
        skip true
        abortOnError false
        report new File(project.buildDir, 'reports/checkstyle')
    }

    // configuration of FindBugs checker
    findbugs {
        skip false
        abortOnError false
        report new File(project.buildDir, 'reports/findbugs')
    }

    // configuration of PMD checker
    pmd {
        skip false
        abortOnError false
        report new File(project.buildDir, 'reports/pmd')
    }
}

// use another plugin for Checkstyle
apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {

//    The Checkstyle plugin does not add the following tasks to the project:
//    checkstyleMain
//    checkstyleTest
//    checkstyleSourceSet
//    opposite to what is said in the docs
//    https://docs.gradle.org/current/userguide/checkstyle_plugin.html

    ignoreFailures = true
    configFile file("${project.rootDir}/config/checkstyle/checkstyle_easy.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

// firebase
apply plugin: 'com.google.gms.google-services'
